; Declare constants for the multiboot header.
MBALIGN   equ  1 << 0             		; align loaded modules on page boundaries
MEMINFO   equ  1 << 1             		; provide memory map
VIDEO     equ  1 << 2			  		; tell GRUB we want to set a video mode
FLAGS     equ  MBALIGN | MEMINFO | VIDEO ; this is the Multiboot 'flag' field
MAGIC     equ  0x1BADB002         		; 'magic number' lets bootloader find the header
CHECKSUM  equ -(MAGIC + FLAGS)    		; checksum of above, to prove we are multiboot

; get GRUB to setup our video mode
VIDEO_MODE   equ 0						; 1 for text mode, 0 for graphical
VIDEO_WIDTH  equ 1920					; number of pixels in graphical mode, num chars in text. 0 if we just want what GRUB gives us
VIDEO_HEIGHT equ 1080					; number of pixels in graphical mode, num chars in text
VIDEO_DEPTH  equ 0						; size of each pixel in graphical mode, 0 for text mode. 0 if we want what GRUB gives us
 
; Declare a multiboot header that marks the program as a kernel. These are magic
; values that are documented in the multiboot standard. The bootloader will
; search for this signature in the first 8 KiB of the kernel file, aligned at a
; 32-bit boundary. The signature is in its own section so the header can be
; forced to be within the first 8 KiB of the kernel file.
section .multiboot
align 4
	dd MAGIC
	dd FLAGS
	dd CHECKSUM

	dd 0
	dd 0
	dd 0
	dd 0

	dd 0
	dd VIDEO_MODE
	dd VIDEO_WIDTH
	dd VIDEO_HEIGHT
	dd VIDEO_DEPTH

; The multiboot standard does not define the value of the stack pointer register
; (esp) and it is up to the kernel to provide a stack. This allocates room for a
; small stack by creating a symbol at the bottom of it, then allocating 16384
; bytes for it, and finally creating a symbol at the top. The stack grows
; downwards on x86. The stack is in its own section so it can be marked nobits,
; which means the kernel file is smaller because it does not contain an
; uninitialized stack. The stack on x86 must be 16-byte aligned according to the
; System V ABI standard and de-facto extensions. The compiler will assume the
; stack is properly aligned and failure to align the stack will result in
; undefined behavior.
section .bss
align 16
stack_bottom:
resb 16384 ; 16 KiB
stack_top:
 
; The linker script specifies _start as the entry point to the kernel and the
; bootloader will jump to this position once the kernel has been loaded. It
; doesn't make sense to return from this function as the bootloader is gone.
; Declare _start as a function symbol with the given symbol size.
section .text
global _start:function (_start.end - _start)
_start:
	; The bootloader has loaded us into 32-bit protected mode on a x86
	; machine. Interrupts are disabled. Paging is disabled. The processor
	; state is as defined in the multiboot standard. The kernel has full
	; control of the CPU. The kernel can only make use of hardware features
	; and any code it provides as part of itself. There's no printf
	; function, unless the kernel provides its own <stdio.h> header and a
	; printf implementation. There are no security restrictions, no
	; safeguards, no debugging mechanisms, only what the kernel provides
	; itself. It has absolute and complete power over the
	; machine.
 
	; This is a good place to initialize crucial processor state before the
	; high-level kernel is entered. It's best to minimize the early
	; environment where crucial features are offline. Note that the
	; processor is not fully initialized yet: Features such as floating
	; point instructions and instruction set extensions are not initialized
	; yet. The GDT should be loaded here. Paging should be enabled here.
	; C++ features such as global constructors and exceptions will require
	; runtime support to work as well.
	cli 
	lgdt [gdt_descriptor]
	jmp 0x08:.load_cs
.load_cs:
	mov ax, 0x10
	mov ds, ax
	mov es, ax
	mov fs, ax
	mov gs, ax
	mov ss, ax

	; To set up a stack, we set the esp register to point to the top of our
	; stack (as it grows downwards on x86 systems). This is necessarily done
	; in assembly as languages such as C cannot function without a stack.
	mov esp, stack_top

	; Enter the high-level kernel. The ABI requires the stack is 16-byte
	; aligned at the time of the call instruction (which afterwards pushes
	; the return pointer of size 4 bytes). The stack was originally 16-byte
	; aligned above and we've since pushed a multiple of 16 bytes to the
	; stack since (pushed 0 bytes so far) and the alignment is thus
	; preserved and the call is well defined.
    ; note, that if you are building on Windows, C functions may have "_" prefix in assembly: _kernel_main
	push eax
	push ebx
	extern kmain
	call kmain
 
	; If the system has nothing more to do, put the computer into a loop
	; until the next interrupt by continuously jumping to the same line
	jmp $
.end:

gdt_start:
    gdt_null:
        dd 0x0          ;dd is a double-word (4 bytes)
        dd 0x0

    gdt_code:
        ;base = 0x0, limit=0xfffff
        ;1st flags: (present) 1, (privilege) 00, (descriptor type) 1
        ;type flags: (code) 1, (conforming) 0, (readable) 1, (accessed) 0
        ;2nd flags: (granularity) 1 (32-bit default) 1, (64-bit seg) 0, (AVL) 0
        dw 0xffff       ;Limit
        dw 0x0          ;Base (bits 0-15)
        db 0x0          ;Base (bits 16-23)
        db 10011010b    ;1st flags, type flags
        db 11001111b    ;2nd flags, Limit (bits 16-19)
        db 0x0          ;Base (bits 24-31)
    
    gdt_data:
        ;type flags: (code) 0, (expand down) 0, (writable) 1, (accessed) 0
        ;everything else is the same
        dw 0xffff
        dw 0x0
        db 0x0
        db 10010010b
        db 11001111b
        db 0x0
    
    gdt_end:


gdt_descriptor:
    dw gdt_end - gdt_start -1   ;size of GDT, always one less than true size
    dd gdt_start                ;start address of the GDT

;GDT segment descriptor offsets
CODE_SEG equ gdt_code - gdt_start
DATA_SEG equ gdt_data - gdt_start